dnl Process this file with autoconf to produce a configure script.
dnl SRE, Mon Oct  5 14:55:45 1998

AC_INIT(src/hmmpfam.c)

echo "   Welcome to HMMER... configuring for your system."

AC_SUBST(MDEFS)
AC_SUBST(PVMLIBDIR)
AC_SUBST(PVMINCDIR)
AC_SUBST(PVMFLAG)
AC_SUBST(PVMPROGS)
AC_SUBST(PVMLIBS)


dnl Checks for programs.
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_RANLIB

dnl Sets the build arena: where we're looking for
dnl module .a and .h files during the build, relative
dnl to top_srcdir: a Makefile.in will set the path
dnl to be top_srcdir/ARENA.
ARENA="lib"
AC_SUBST(ARENA)

dnl GNU make check; makefile dependency lines for executables.
dnl original from John Darrington <j.darrington@elvis.murdoch.edu.au> 
dnl w/ heavy modifications from me.
dnl We need this because GNU make and SYSV make use different systems
dnl specifying variables for dependencies: $$@ in sysv, %: %.o in GNU.
dnl Would love to hear a better way of doing this.
dnl 
AC_DEFUN(CHECK_GNU_MAKE,[ 
  AC_MSG_CHECKING(whether your make is GNU make)
  foundGNUmake='nope.' ;
  STD_EXEC_DEPENDENCY=[\$\$\@.o] ;
  if ( make --version nothing 2> /dev/null | grep GNU > /dev/null ) ;  then
     foundGNUmake='yes, it is.' ;
     STD_EXEC_DEPENDENCY='%: %.o' ;
  fi
  AC_MSG_RESULT($foundGNUmake)
  AC_SUBST(STD_EXEC_DEPENDENCY)
])
CHECK_GNU_MAKE


dnl Configure PVM
dnl
AC_ARG_WITH(pvm, 
[  --with-pvm         enable PVM, Parallel Virtual Machine],
[case $with_pvm in
   yes)  echo 'Configuring for PVM'
 	 PVMLIBDIR="-L${PVM_ROOT}/lib/${PVM_ARCH}"
	 PVMINCDIR="-I${PVM_ROOT}/include"
	 PVMFLAG="-DHMMER_PVM"
	 PVMPROGS="hmmcalibrate-pvm hmmpfam-pvm hmmsearch-pvm"
	 PVMLIBS="-lpvm3"
	 ;;
   no)   ;;
   *)    echo "Ignoring unknown argument to --with-pvm: $with_pvm"
	 ;;
esac])

dnl Enable threads
AC_ARG_ENABLE(threads, 
[  --disable-threads  disable POSIX threads support],
[case $enable_threads in 
   yes) echo "Enabling POSIX threads support"
	AC_CHECK_LIB(pthread, pthread_create, 
	[ LIBS="${LIBS} -lpthread"
	  MDEFS="${MDEFS} -DHMMER_THREADS"

	])
	;;
   no)  echo "POSIX threads support disabled"
        ;;
   *)   echo "Ignoring unknown argument to --disable-threads: $enable_threads"
	;;
esac],
[AC_CHECK_LIB(pthread, pthread_create, 
	[ echo "   Enabling POSIX threads support by default"
	  LIBS="${LIBS} -lpthread"
	  MDEFS="${MDEFS} -DHMMER_THREADS"
	])
])

dnl Checks for system libraries
AC_CHECK_LIB(m, sqrt, LIBS="${LIBS} -lm")

dnl Check for functions
AC_CHECK_FUNCS(pthread_setconcurrency)
AC_CHECK_FUNCS(pthread_attr_setscope)

dnl Doublecheck important sizeof's for binary files
dnl
AC_CHECK_SIZEOF(unsigned int, 4)
if test "$ac_cv_sizeof_unsigned_int" != "4"; then
  AC_MSG_WARN "sizeof(unsigned int) isn't 4. you will have problems with binary files."
fi

AC_CHECK_SIZEOF(unsigned short, 2)
if test "$ac_cv_sizeof_unsigned_short" != "2"; then
  AC_MSG_WARN "sizeof(unsigned short) isn't 2. you will have problems with binary files."
fi

dnl Write out Makefiles
echo "   Configuration complete. Writing Makefiles and such..."
AC_OUTPUT(Makefile src/Makefile lib/Makefile lib/squid/Makefile testsuite/Makefile)


