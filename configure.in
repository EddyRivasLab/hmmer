dnl Process this file with autoconf to produce a configure script.
dnl SRE, Mon Oct  5 14:55:45 1998

AC_INIT(src/hmmpfam.c)

echo "   Welcome to HMMER... configuring for your system."

AC_SUBST(MDEFS)
AC_SUBST(PVMLIBDIR)
AC_SUBST(PVMINCDIR)
AC_SUBST(PVMFLAG)
AC_SUBST(PVMPROGS)
AC_SUBST(PVMLIBS)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_RANLIB

dnl GNU make check; makefile dependency lines for executables.
dnl original from John Darrington <j.darrington@elvis.murdoch.edu.au> 
dnl w/ heavy modifications from me.
dnl We need this because GNU make and SYSV make use different systems
dnl specifying variables for dependencies: $$@ in sysv, %: %.o in GNU.
dnl Would love to hear a better way of doing this.
dnl 
dnl I use different conventions in my Makefiles. Sometimes 
dnl executable "foo" has a file "foo.c" - this is the HMMER convention.
dnl Sometimes executable "foo" has a file "foo_main.c" - this is
dnl the SQUID convention. The configure script can deal with
dnl either; Makefile.in contains one of HMMER_EXEC_DEPENDENCY or
dnl SQUID_EXEC_DEPENDENCY.
dnl
AC_DEFUN(CHECK_GNU_MAKE,[ 
  AC_MSG_CHECKING(whether your make is GNU make)
  foundGNUmake='nope, assuming sysv make.' ;
  HMMER_EXEC_DEPENDENCY=[\$\$\@.o] ;
  SQUID_EXEC_DEPENDENCY=[\$\$\@_main.o] ;
  if ( make --version nothing 2> /dev/null | grep GNU > /dev/null ) ;  then
     foundGNUmake='yes, it is.' ;
     HMMER_EXEC_DEPENDENCY='%: %.o' ;
     SQUID_EXEC_DEPENDENCY='%: %_main.o' ;
  fi
  AC_MSG_RESULT($foundGNUmake)
  AC_SUBST(HMMER_EXEC_DEPENDENCY)
  AC_SUBST(SQUID_EXEC_DEPENDENCY)
])
CHECK_GNU_MAKE


dnl Configure PVM
dnl
AC_ARG_WITH(pvm, 
[  --with-pvm         enable PVM, Parallel Virtual Machine],
[case $with_pvm in
   yes)  echo 'Configuring for PVM'
 	 PVMLIBDIR="-L${PVM_ROOT}/lib/${PVM_ARCH}"
	 PVMINCDIR="-I${PVM_ROOT}/include"
	 PVMFLAG="-DHMMER_PVM"
	 PVMPROGS="hmmcalibrate-pvm hmmpfam-pvm hmmsearch-pvm"
	 PVMLIBS="-lpvm3"
	 ;;
   no)   ;;
   *)    echo "Ignoring unknown argument to --with-pvm: $with_pvm"
	 ;;
esac])

dnl Enable threads
AC_ARG_ENABLE(threads, 
[  --disable-threads  disable POSIX threads support],
[case $enable_threads in 
   yes) echo "Enabling POSIX threads support"
	AC_CHECK_LIB(pthread, pthread_create, 
	[ LIBS="${LIBS} -lpthread"
	  MDEFS="${MDEFS} -DHMMER_THREADS"

	])
	;;
   no)  echo "POSIX threads support disabled"
        ;;
   *)   echo "Ignoring unknown argument to --disable-threads: $enable_threads"
	;;
esac],
[AC_CHECK_LIB(pthread, pthread_create, 
	[ echo "   Enabling POSIX threads support by default"
	  LIBS="${LIBS} -lpthread"
	  MDEFS="${MDEFS} -DHMMER_THREADS"
	])
])

dnl Checks for system libraries
AC_CHECK_LIB(m, sqrt)

dnl Check for functions
AC_CHECK_FUNCS(pthread_setconcurrency)
AC_CHECK_FUNCS(pthread_attr_setscope)

dnl Although we configure our fixed-size integers in squid,
dnl we do need some of that configuration code here, because
dnl we might (on Solaris, for instance) need to include
dnl -lsocket to the linker to get ntohs() and friends.
dnl
AC_CHECK_FUNCS(ntohs, , AC_CHECK_LIB(socket, ntohs))
AC_CHECK_FUNCS(ntohl, , AC_CHECK_LIB(socket, ntohl))
AC_CHECK_FUNCS(htons, , AC_CHECK_LIB(socket, htons))
AC_CHECK_FUNCS(htonl, , AC_CHECK_LIB(socket, htonl))

dnl Configure the squid library
dnl
AC_CONFIG_SUBDIRS(lib/squid)

dnl Write out Makefiles
dnl
echo "   Configuration complete. Writing Makefiles and such..."
AC_OUTPUT(Makefile src/Makefile\
          lib/Makefile\
          testsuite/Makefile)


